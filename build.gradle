// Top-level build file where you can add configuration options common to all sub-projects/modules.
// 配置插件依赖仓库
buildscript {
    repositories {
        mavenCentral()
        mavenLocal()
        google()
    }
    dependencies {
        classpath 'com.github.kezong:fat-aar:1.3.8'
        // Kotlin Javadoc，非必须
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:1.6.20"
    }
}

plugins {
    id 'com.android.application' version '7.3.0' apply false
    id 'com.android.library' version '7.3.0' apply false
    id 'org.jetbrains.kotlin.android' version '1.9.0' apply false
    id 'com.google.gms.google-services' version '4.3.15' apply false
    id("com.gradleup.nmcp").version("0.0.8")
}

// read local properties
File localPropsFile = project.rootProject.file('maven.properties')
if (localPropsFile.exists()) {
    Properties p = new Properties()
    new FileInputStream(localPropsFile).withCloseable { is ->
        p.load(is)
    }
    p.each { name, value ->
        ext[name] = value
    }
}


nmcp {
    publishAllProjectsProbablyBreakingProjectIsolation {
        username = sonatypeUsername
        password = sonatypePassword
        // publish manually from the portal
        publicationType = "USER_MANAGED"
        // or if you want to publish automatically
        //publicationType = "AUTOMATIC"
    }
}


apply from: "config.gradle"